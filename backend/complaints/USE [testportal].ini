USE [testportal]
GO
/****** Object:  StoredProcedure [dbo].[GetComplaintsFull]    Script Date: 24.09.2025 11:56:31 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[GetComplaintsFull]
    @Dealer UNIQUEIDENTIFIER = NULL
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @Today DATE = GETDATE();  -- кеш дати

    SELECT 
        C.[id],
        C.[web_number] AS WebNumber,
        C.[order_number] AS OrderNumber,
        C.[complaint_date] AS ComplaintDate,
        C.[description] AS DescriptionComplaint,
        U.[full_name] AS FullName,
        S.[Наименование] AS SolutionName,
        C.[order_define_date] AS OrderDefineDate,
        C.[order_deliver_date] AS OrderDeliveDate,
        P.[Номер] AS ComplaintNumber,
        P.[ПутиРешения] AS Solution,
        ZC.[Номер] AS NumberComplaints,
        ZC.[ДатаОтгрузки] AS DataComplete,
        M.[LastManagerName],
        M.[ГраничнаяДатаВозврата] AS LastDateReturn,
        I.[Наименование] AS IssueName,
        CS.[SeriesList],
        CP.Photos AS PhotoLinks,  -- всі фото об'єднані
        CASE 
            WHEN P.ДатаОтказа > '2001-01-01' THEN N'Відмова'
            WHEN P.Устранено > '2001-01-01' OR P.ДатаУстранения > '2001-01-01' THEN N'Вирішено'
            WHEN P.ДатаПолучения > '2001-01-01' THEN N'На складі'
            WHEN P.ДатаВРаботу > '2001-01-01' THEN N'Виробництво'
            WHEN OPS.Дата IS NOT NULL OR ZP.ДатаПередачиНаСклад IS NOT NULL THEN N'На складі'
            WHEN R.Дата IS NOT NULL THEN N'Вирішено'
            WHEN CAST(ZP.ДатаЗапуска AS DATE) = @Today THEN N'Виробництво'
            ELSE N'Обробка'
        END AS StatusName
    FROM [testportal].[dbo].[Complaints] C
    LEFT JOIN [testportal].[dbo].[Users] U 
        ON C.user_id_1C = U.user_id_1C
    LEFT JOIN [oknastyle_biV2].[dbo].[Справочники.GM_РешенияЗадачРекламаций] S 
        ON S.Ссылка = C.solution
    LEFT JOIN [oknastyle_biV2].[dbo].[Справочники.GM_ЗадачиРекламаций] I 
        ON I.Ссылка = C.issue

    -- всі фото об'єднані в один рядок через STRING_AGG
    OUTER APPLY (
        SELECT STRING_AGG(CP2.[photo], ',') AS Photos
        FROM [testportal].[dbo].[ComplaintPhotos2] CP2
        WHERE CP2.complaint_id = C.id
    ) CP

    -- серії
    OUTER APPLY (
        SELECT STRING_AGG(CS.[serie_name], ', ') AS SeriesList
        FROM [testportal].[dbo].[ComplaintOrderSeries] CS
        WHERE CS.complaint_id = C.id
    ) CS

    -- претензія
    OUTER APPLY (
        SELECT TOP 1 ДатаПолучения, ДатаВРаботу, ДатаУстранения, Устранено, Контрагент, 
                     ДатаОтказа, ЗаказПокупателяПретензия, НомерWEB, Номер, [ПутиРешения]
        FROM [oknastyle_biV2].[dbo].[Документы.БВ_Претензия] P
        WHERE P.Контрагент = C.user_id_1C 
          AND P.НомерWEB = C.web_number
          AND P.Проведен = 0x01 AND P.ПометкаУдаления = 0x00 AND P.Дозаказ = 0 
        ORDER BY ДатаУстранения DESC, ДатаВРаботу DESC
    ) P

    -- відвантаження
    OUTER APPLY (
        SELECT TOP 1 ДатаОтгрузки, Ссылка, Номер
        FROM [oknastyle_biV2].[dbo].[Документы.ЗаказПокупателя] ZC
        WHERE ZC.Контрагент =  C.user_id_1C 
          AND ZC.Ссылка = P.ЗаказПокупателяПретензия
          AND ZC.Проведен = 0x01 AND ZC.ПометкаУдаления = 0x00
        ORDER BY ДатаОтгрузки DESC
    ) ZC

    -- продукція по замовленню
    LEFT JOIN [oknastyle_biV2].[dbo].[Документы.ЗаказНаПроизводство.Продукция] ZPP
        ON P.ЗаказПокупателяПретензия = ZPP.Заказ

    -- виробництво
    OUTER APPLY (
        SELECT TOP 1 ДатаПередачиНаСклад, ДатаЗапуска, Ссылка
        FROM [oknastyle_biV2].[dbo].[Документы.ЗаказНаПроизводство] ZP
        WHERE ZP.Ссылка = ZPP.Ссылка
          AND ZP.Проведен = 0x01 AND ZP.ПометкаУдаления = 0x00
        ORDER BY ДатаПередачиНаСклад DESC
    ) ZP

    -- продукція зі зміни
    LEFT JOIN [oknastyle_biV2].[dbo].[Документы.ОтчетПроизводстваЗаСмену.Продукция] RP
        ON RP.Заказ = ZP.Ссылка

    -- звіт за зміну
    OUTER APPLY (
        SELECT TOP 1 Дата, Ссылка
        FROM [oknastyle_biV2].[dbo].[Документы.ОтчетПроизводстваЗаСмену] OPS
        WHERE OPS.Ссылка = RP.Ссылка
          AND OPS.Проведен = 0x01 AND OPS.ПометкаУдаления = 0x00
        ORDER BY Дата DESC
    ) OPS

    -- реалізація
    OUTER APPLY (
        SELECT TOP 1 Дата
        FROM [oknastyle_biV2].[dbo].[Документы.РеализацияТоваровУслуг] R
        WHERE R.Контрагент =  C.user_id_1C 
          AND R.Сделка = P.ЗаказПокупателяПретензия
          AND R.Сделка <> 0x00000000000000000000000000000000
          AND R.Проведен = 0x01 AND R.ПометкаУдаления = 0x00
        ORDER BY Дата DESC
    ) R

    -- менеджери
    OUTER APPLY (
        SELECT TOP(1)
            COALESCE(Hist.ManagerName, KMMain.ManagerName) AS LastManagerName,
            CASE
                WHEN S2.[ГраничнаяДатаВозврата] IS NULL OR CONVERT(datetime, S2.[ГраничнаяДатаВозврата]) = '2001-01-01'
                THEN NULL
                ELSE S2.[ГраничнаяДатаВозврата]
            END AS ГраничнаяДатаВозврата
        FROM [oknastyle_biV2].[dbo].[Документы.БВ_Претензия] P2
        LEFT JOIN [oknastyle_biV2].[dbo].[Документы.БВ_Претензия.СпособыРешения] S2
            ON S2.[Ссылка] = P2.[Ссылка]
        
        OUTER APPLY (
            SELECT TOP(1) F.Наименование AS ManagerName
            FROM [oknastyle_biV2].[dbo].[Справочники.Контрагенты.ИсторияИзмененияМенеджеров] HIM
            JOIN [oknastyle_biV2].[dbo].[Справочники.Пользователи] U
                ON HIM.Менеджер = U.Ссылка AND U.ПометкаУдаления = 0
            LEFT JOIN [oknastyle_biV2].[dbo].[Справочники.ФизическиеЛица] F
                ON U.ФизЛицо = F.Ссылка
            WHERE HIM.[Ссылка] = C.user_id_1C
            ORDER BY HIM.[ДатаС] DESC
        ) AS Hist
        
        OUTER APPLY (
            SELECT TOP(1) F2.Наименование AS ManagerName
            FROM [oknastyle_biV2].[dbo].[Справочники.Контрагенты.МенеджерыПокупателя] KM
            JOIN [oknastyle_biV2].[dbo].[Справочники.Пользователи] U2
                ON KM.МенеджерПокупателя = U2.Ссылка AND U2.ПометкаУдаления = 0
            LEFT JOIN [oknastyle_biV2].[dbo].[Справочники.ФизическиеЛица] F2
                ON U2.ФизЛицо = F2.Ссылка
            WHERE KM.[Ссылка] = C.user_id_1C
              AND KM.[ОсновнойПоОрганизации] = 0xA88A001B214EEE7A11E15373F04D8182
        ) AS KMMain
    ) M

    WHERE (@Dealer IS NULL OR C.user_id_1C = @Dealer)
    ORDER BY P.НомерWEB;
END;






USE [testportal]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[GetComplaintsFull]
    @Dealer UNIQUEIDENTIFIER = NULL
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @Today DATE = GETDATE();

    -- Основний запит
    SELECT 
        C.[id],
        C.[web_number] AS WebNumber,
        C.[order_number] AS OrderNumber,
        C.[complaint_date] AS ComplaintDate,
        C.[description] AS DescriptionComplaint,
        U.[full_name] AS FullName,
        S.[Наименование] AS SolutionName,
        C.[order_define_date] AS OrderDefineDate,
        C.[order_deliver_date] AS OrderDeliveDate,
        P.[Номер] AS ComplaintNumber,
        P.[ПутиРешения] AS Solution,
        ZC.[Номер] AS NumberComplaints,
        ZC.[ДатаОтгрузки] AS DataComplete,
        M.[LastManagerName],
        M.[ГраничнаяДатаВозврата] AS LastDateReturn,
        I.[Наименование] AS IssueName,
        CS.[SeriesList],
        CP.[PhotoLinks],

        ISNULL(Produced.ProducedTotal,0) AS КоличествоПроизведено,
        ISNULL(Sold.SoldTotal,0) AS КоличествоРеализовано,

        CASE 
            WHEN ISNULL(Sold.SoldTotal,0) >= ZC.ГМ_КоличествоКонструкцийИДопов THEN N'Вирішено'
            WHEN ISNULL(Produced.ProducedTotal,0) >= ZC.ГМ_КоличествоКонструкцийИДопов THEN N'На складі'
            WHEN P.ДатаОтказа > '2001-01-01' THEN N'Відмова'
            WHEN P.Устранено > '2001-01-01' OR P.ДатаУстранения > '2001-01-01' THEN N'Вирішено'
            WHEN P.ДатаПолучения > '2001-01-01' THEN N'На складі'
            WHEN P.ДатаВРаботу > '2001-01-01' THEN N'Виробництво'
            WHEN OPS.Дата IS NOT NULL OR ZP.ДатаПередачиНаСклад IS NOT NULL THEN N'На складі'
            WHEN R.Дата IS NOT NULL THEN N'Вирішено'
            WHEN CAST(ZP.ДатаЗапуска AS DATE) = @Today THEN N'Виробництво'
            ELSE N'Обробка'
        END AS StatusName

    FROM [testportal].[dbo].[Complaints] C
    LEFT JOIN [testportal].[dbo].[Users] U
        ON C.user_id_1C = U.user_id_1C
    LEFT JOIN [oknastyle_biV2].[dbo].[Справочники.GM_РешенияЗадачРекламаций] S
        ON S.Ссылка = C.solution
    LEFT JOIN [oknastyle_biV2].[dbo].[Справочники.GM_ЗадачиРекламаций] I
        ON I.Ссылка = C.issue

    -- агреговані фото
    OUTER APPLY (
        SELECT STRING_AGG(CP2.[photo], ',') AS PhotoLinks
        FROM [testportal].[dbo].[ComplaintPhotos2] CP2
        WHERE CP2.complaint_id = C.id
    ) CP

    -- агреговані серії
    OUTER APPLY (
        SELECT STRING_AGG(CS.[serie_name], ', ') AS SeriesList
        FROM [testportal].[dbo].[ComplaintOrderSeries] CS
        WHERE CS.complaint_id = C.id
    ) CS

    -- претензія
    OUTER APPLY (
        SELECT TOP 1 ДатаПолучения, ДатаВРаботу, ДатаУстранения, Устранено, Контрагент, 
                     ДатаОтказа, ЗаказПокупателяПретензия, НомерWEB, Номер, [ПутиРешения]
        FROM [oknastyle_biV2].[dbo].[Документы.БВ_Претензия] P2
        WHERE P2.Контрагент = C.user_id_1C
          AND P2.НомерWEB = C.web_number
          AND P2.Проведен = 0x01 AND P2.ПометкаУдаления = 0x00 AND P2.Дозаказ = 0
        ORDER BY ДатаУстранения DESC, ДатаВРаботу DESC
    ) P

    -- відвантаження
    OUTER APPLY (
        SELECT TOP 1 ДатаОтгрузки, Ссылка, Номер, ГМ_КоличествоКонструкцийИДопов
        FROM [oknastyle_biV2].[dbo].[Документы.ЗаказПокупателя] ZC2
        WHERE ZC2.Контрагент =  C.user_id_1C
          AND ZC2.Ссылка = P.ЗаказПокупателяПретензия
          AND ZC2.Проведен = 0x01 AND ZC2.ПометкаУдаления = 0x00
        ORDER BY ДатаОтгрузки DESC
    ) ZC

    -- виробництво агреговано
    OUTER APPLY (
        SELECT SUM(RP2.Количество) AS ProducedTotal
        FROM [oknastyle_biV2].[dbo].[Документы.ОтчетПроизводстваЗаСмену.Продукция] RP2
        JOIN [oknastyle_biV2].[dbo].[Документы.ЗаказНаПроизводство.Продукция] ZPP2
            ON ZPP2.Ссылка = RP2.Заказ
        WHERE ZPP2.Заказ = P.ЗаказПокупателяПретензия
    ) Produced

    -- реалізація агреговано
    OUTER APPLY (
        SELECT SUM(T.Количество) AS SoldTotal
        FROM [oknastyle_biV2].[dbo].[Документы.РеализацияТоваровУслуг.Товары] T
        JOIN [oknastyle_biV2].[dbo].[Документы.РеализацияТоваровУслуг] R2
            ON R2.Ссылка = T.Ссылка
        WHERE R2.Сделка = P.ЗаказПокупателяПретензия
    ) Sold

    -- менеджери
    OUTER APPLY (
        SELECT TOP(1)
            COALESCE(Hist.ManagerName, KMMain.ManagerName) AS LastManagerName,
            CASE
                WHEN S2.[ГраничнаяДатаВозврата] IS NULL OR CONVERT(datetime, S2.[ГраничнаяДатаВозврата]) = '2001-01-01'
                THEN NULL
                ELSE S2.[ГраничнаяДатаВозврата]
            END AS ГраничнаяДатаВозврата
        FROM [oknastyle_biV2].[dbo].[Документы.БВ_Претензия] P2
        LEFT JOIN [oknastyle_biV2].[dbo].[Документы.БВ_Претензия.СпособыРешения] S2
            ON S2.[Ссылка] = P2.[Ссылка]

        OUTER APPLY (
            SELECT TOP(1) F.Наименование AS ManagerName
            FROM [oknastyle_biV2].[dbo].[Справочники.Контрагенты.ИсторияИзмененияМенеджеров] HIM
            JOIN [oknastyle_biV2].[dbo].[Справочники.Пользователи] U
                ON HIM.Менеджер = U.Ссылка AND U.ПометкаУдаления = 0
            LEFT JOIN [oknastyle_biV2].[dbo].[Справочники.ФизическиеЛица] F
                ON U.ФизЛицо = F.Ссылка
            WHERE HIM.[Ссылка] = C.user_id_1C
            ORDER BY HIM.[ДатаС] DESC
        ) AS Hist

        OUTER APPLY (
            SELECT TOP(1) F2.Наименование AS ManagerName
            FROM [oknastyle_biV2].[dbo].[Справочники.Контрагенты.МенеджерыПокупателя] KM
            JOIN [oknastyle_biV2].[dbo].[Справочники.Пользователи] U2
                ON KM.МенеджерПокупателя = U2.Ссылка AND U2.ПометкаУдаления = 0
            LEFT JOIN [oknastyle_biV2].[dbo].[Справочники.ФизическиеЛица] F2
                ON U2.ФизЛицо = F2.Ссылка
            WHERE KM.[Ссылка] = C.user_id_1C
              AND KM.[ОсновнойПоОрганизации] = 0xA88A001B214EEE7A11E15373F04D8182
        ) AS KMMain
    ) M

    -- остання реалізація
    OUTER APPLY (
        SELECT TOP 1 Дата
        FROM [oknastyle_biV2].[dbo].[Документы.РеализацияТоваровУслуг] R
        WHERE R.Контрагент = C.user_id_1C
            AND R.Сделка = P.ЗаказПокупателяПретензия
            AND R.Сделка <> 0x00000000000000000000000000000000
            AND R.Проведен = 0x01 AND R.ПометкаУдаления = 0x00
        ORDER BY Дата DESC
    ) R


    LEFT JOIN [oknastyle_biV2].[dbo].[Документы.ЗаказНаПроизводство] ZP
        ON ZP.Ссылка = P.ЗаказПокупателяПретензия

    LEFT JOIN [oknastyle_biV2].[dbo].[Документы.ОтчетПроизводстваЗаСмену] OPS
        ON OPS.Ссылка = ZP.Ссылка

    WHERE (@Dealer IS NULL OR C.user_id_1C = @Dealer)
    ORDER BY P.НомерWEB;

END
GO
